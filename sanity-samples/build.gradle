/*
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 */

apply plugin: "java"
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'io.pravega.samples.flink.testruns.StandardReaderWriterTest'
applicationDefaultJvmArgs = ["-Dlog4j.configuration=file:conf/log4j.properties"]

configurations {
    shadowRelocate {
    }
}

dependencies {
    compile "org.slf4j:slf4j-log4j12:1.7.14"
    compile "io.pravega:pravega-connectors-flink_2.11:${pravegaConnectorsVersion}"
    compile "org.apache.flink:flink-streaming-java_2.11:${flinkVersion}"
    compile "org.apache.flink:flink-test-utils_2.11:${flinkVersion}"
    if (includeNautilusCredentials.toBoolean()) {
        shadowRelocate "com.emc.nautilus:pravega-credentials:${nautilusCredentials}"
    }
}

shadowJar {
    configurations = [project.configurations.runtime, project.configurations.shadowRelocate]

    dependencies {
        include dependency("io.pravega:pravega-connectors-flink_2.11:${pravegaConnectorsVersion}")
        include dependency("org.apache.flink:flink-test-utils-junit:${flinkVersion}")
        if (includeNautilusCredentials.toBoolean()) {
            include dependency("com.emc.nautilus:pravega-credentials:${nautilusCredentials}")
        }
        include dependency("junit:junit:4.12")
    }
    mergeServiceFiles()

}

distributions {
    main {
        contents {
            into('lib') {
                from shadowJar
                from(project.configurations.shadow)
            }
        }
    }
}

run {
    standardInput = System.in

    if (project.hasProperty("argsList")) {
        args(argsList.split(' '))
    }

}